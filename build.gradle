buildscript {
    ext.kotlin_version = '1.2.0'
    ext.dagger_version = '2.13'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

apply plugin: 'kotlin-kapt'

kapt {
    javacOptions {
        // Increase the max count of errors from annotation processors.
        // Default is 100.
        option("-Xmaxerrs", 500)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'io.github.erikhuizinga.kotfee.CoffeeApp'
    }

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile group: 'com.google.dagger', name: 'dagger', version: dagger_version
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}